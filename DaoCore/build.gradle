task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.+'
    }
}

apply plugin: 'android-library'
apply plugin: 'maven'


repositories {
    maven { url 'http://repo1.maven.org/maven2' }
}

android {
    compileSdkVersion 18
    buildToolsVersion "21.1.1"

    defaultConfig {
        minSdkVersion 4
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
}

def deployToProd = hasProperty("pathMaven.deployToProd") && project.ext["pathMaven.deployToProd"].toBoolean()
println "will deploy to prod?" + deployToProd

uploadArchives {
    repositories {
        mavenDeployer {
            if(deployToProd) {
                snapshotRepository(url: project.ext["pathMaven.repo.snapshots"]) {
                    authentication(userName: project.ext["pathMaven.username"], password: project.ext["pathMaven.password"]);
                }

                repository(url: project.ext["pathMaven.repo.releases"]) {
                    authentication(userName: project.ext["pathMaven.username"], password: project.ext["pathMaven.password"]);
                }
            } else {
                repository(url: mavenLocal().getUrl())
            }

            pom.project {
                groupId 'de.greenrobot.dao'
                artifactId 'core'
                version '1.1.4'
            }
        }
    }
}
